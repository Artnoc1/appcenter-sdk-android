// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply from: 'versions.gradle'
apply from: 'publish.gradle'

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'

        //noinspection GradleDependency upgrading this is not compatible with the pinned firebase versions
        classpath 'com.google.gms:google-services:4.0.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
    }
}

def localProperties = new Properties()
def file = rootProject.file('local.properties')
if (file.exists()) {
    localProperties.load(file.newDataInputStream())
}
def azureArtifactsGradleAccessToken = localProperties['azure.artifacts.gradle.access.token']

allprojects {
    repositories {
        maven {
            url 'https://identitydivision.pkgs.visualstudio.com/_packaging/AndroidADAL/maven/v1'
            credentials {
                username "AZURE_ARTIFACTS"
                password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "${azureArtifactsGradleAccessToken}"
            }
        }
        google()
        jcenter()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            def config = android.defaultConfig
            def ext = rootProject.ext
            if (android.compileSdkVersion == null)
                android.compileSdkVersion ext.compileSdkVersion
            if (config.minSdkVersion == null)
                config.minSdkVersion ext.minSdkVersion
            if (config.targetSdkVersion == null)
                config.targetSdkVersion ext.targetSdkVersion
            if (config.versionCode == null)
                config.versionCode ext.versionCode
            if (config.versionName == null)
                config.versionName ext.versionName
            if (config.testInstrumentationRunner == null)
                config.testInstrumentationRunner ext.testInstrumentationRunner
        }
    }
}

ext {
    testInstrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'
}

task clean(type: Delete) {
    delete project.buildDir
}